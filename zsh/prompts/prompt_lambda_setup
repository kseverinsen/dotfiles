# vim: syntax=zsh

#
# Lambda+
# A simple theme inspired by lambda from oh-my-zsh and minimal from prezto with
# some additonal tweaks.
#
# Lambda+ Author:
#   Per Øyvind Kanestrøm <perokane@gmail.com>
#
# Lambda author:
#   Nicolas A. Tonne <nixo@gmx.com>
#
# Minimal Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}'
  fi
}

function prompt_lambda_precmd {
  vcs_info
}

function prompt_lambda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_lambda_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '*'
  zstyle ':vcs_info:*' unstagedstr '%F{yellow}'
  zstyle ':vcs_info:*' formats ' %F{blue}%u%b%c%f'
  zstyle ':vcs_info:*' actionformats " %b%c%u|%F{cyan}%a%f"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  #PROMPT='%F{green}%(?::%F{red})λ%f %~${vcs_info_msg_0_} '
  # Define prompts.
  NEWLINE=$'\n'
  PROMPT='%(1j.jobs=%j.) %~${vcs_info_msg_0_}${NEWLINE} %F{green}%(?::%F{red})λ%f '
  RPROMPT=''
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_lambda_setup "$@"

